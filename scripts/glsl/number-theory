// https://arxiv.org/pdf/1806.06725.pdf
// TODO: implement quasiperiodicity, a la https://mathworld.wolfram.com/JacobiThetaFunctions.html
#function theta1
#requires cexp cpow
vec2 theta1(vec2 z, vec2 t) {
	

	vec2 q = cexp(PI * vec2(-t.y,t.x));
	vec2 q4 = cexp(PI/4.0 * vec2(-t.y,t.x));
	vec2 w = cexp(PI * vec2(-z.y,z.x));
	vec2 v = cdiv(1.0,w);
	vec2 summer = w-v;
	float alternate_j = -1.0;
	for (int j = 1; j < THETA_BOUND; j++) {
		summer += alternate_j * cmul(cpow(q,float(j*(j+1))),cpow(w,float(2*j+1))-cpow(v,float(2*j+1)));
		alternate_j = -alternate_j;
	}
	return cmul(vec2(q4.y,-q4.x),summer);
}
#endfunction



#function theta2
#requires cexp cpow
vec2 theta2(vec2 z, vec2 t) {
	vec2 q = cexp(PI * vec2(-t.y,t.x));
	vec2 q4 = cexp(PI/4.0 * vec2(-t.y,t.x));
	vec2 w = cexp(PI * vec2(-z.y,z.x));
	vec2 v = cdiv(1.0,w);
	vec2 summer = w+v;
	for (int j = 1; j < THETA_BOUND; j++) {
		summer += cmul(cpow(q,float(j*(j+1))),cpow(w,float(2*j+1))+cpow(v,float(2*j+1)));
	}
	return cmul(q4,summer);
}
#endfunction



#function theta3
#requires cexp cpow
vec2 theta3(vec2 z, vec2 t) {
	vec2 q = cexp(PI * vec2(-t.y,t.x));
	vec2 w = cexp(PI * vec2(-z.y,z.x));
	vec2 v = cdiv(1.0,w);
	vec2 summer = ONE;
	for (int j = 1; j < THETA_BOUND; j++) {
		summer += cmul(cpow(q,float(j*j)),cpow(w,float(2*j))+cpow(v,float(2*j)));
	}
	return summer;
}
#endfunction



#function theta4
#requires cexp cpow
vec2 theta4(vec2 z, vec2 t) {
	vec2 q = cexp(PI * vec2(-t.y,t.x));
	vec2 w = cexp(PI * vec2(-z.y,z.x));
	vec2 v = cdiv(1.0,w);
	float alternate_j = -1.0;
	vec2 summer = ONE;
	for (int j = 1; j < THETA_BOUND; j++) {
		summer += alternate_j * cmul(cpow(q,float(j*j)),cpow(w,float(2*j))+cpow(v,float(2*j)));
		alternate_j = -alternate_j;
	}
	return summer;
}
#endfunction



// auxiliary function
#function eisenstein4
#requires cexp cpow
vec2 eisenstein4(vec2 z)
{
	if (z.y <= 0.0)
	{
		return ZERO;
	}
	vec2 q = cexp(PI * vec2(-z.y,z.x));
	vec2 q4 = cexp(PI/4.0 * vec2(-z.y,z.x));

	vec2 a = ONE;
	vec2 b = ZERO;
	vec2 c = ZERO;

	float alternate_j = -1.0;
	vec2 qjj = ZERO;
	for (int j = 1; j < THETA_BOUND; j++) {
		qjj = cpow(q,float(j*j));

		a += cpow(q,float(j*(j+1)));
		b += qjj;
		c += alternate_j * qjj;

		alternate_j = -alternate_j;
	}

	a = 2.0 * cmul(q4,a);
	b = 2.0 * b + ONE;
	c = 2.0 * c + ONE;

	return (cpow(a,8.0)+cpow(b,8.0)+cpow(c,8.0))/2.0;
}
#endfunction



#function g2
#requires eisenstein4
vec2 g2(vec2 z) {
	// 60.0 *2.0 * pi^4/90.0 = 129.878788045336582
	return 129.878788045336582 * eisenstein4(z);
}
#endfunction



#function eisenstein6
#requires cexp cpow
vec2 eisenstein6(vec2 z)
{
	if (z.y <= 0.0)
	{
		return ZERO;
	}
	vec2 q = cexp(PI * vec2(-z.y,z.x));
	vec2 q4 = cexp(PI/4.0 * vec2(-z.y,z.x));

	vec2 a = ONE;
	vec2 b = ZERO;
	vec2 c = ZERO;

	float alternate_j = -1.0;
	vec2 qjj = ZERO;
	for (int j = 1; j < THETA_BOUND; j++) {
		qjj = cpow(q,float(j*j));
		a += cpow(q,float(j*(j+1)));
		b += qjj;
		c += alternate_j * qjj;

		alternate_j = -alternate_j;
	}

	a = 2.0 * cmul(q4,a);
	b = 2.0 * b + ONE;
	c = 2.0 * c + ONE;

	vec2 summer = -3.0 * cmul(cpow(a,8.0),cpow(b,4.0) + cpow(c,4.0));
	summer += cpow(b,12.0) + cpow(c,12.0);
	return 0.5 * summer;
}
#endfunction



#function g3
#requires eisenstein6
vec2 g3(vec2 z) {
	// off by about 0.5% at z = rho; could use some attention to precision
	// 140.0 *2.0 * pi^6/945.0  = 284.856...
	return 284.856057355645759120065 * eisenstein6(z);
}
#endfunction


// Developer note: eisenstein8 was implemented along the lines of the two above... it's extremely unstable!
// Just use M_k = {E_4,E_6} for all E_k, k>6.

// Returns E_k(z) where E_k is the normalized Eisenstein series of weight k and level 1 (k must be even...).
// Uses equation (1.3) from https://arxiv.org/pdf/math/0009130.pdf.
//TODO: implement eisenstein(k,q)
#function eisenstein
#requires eisenstein4 eisenstein6 cpow
vec2 eisenstein(float k, vec2 z)
{
	if (z.y <= 0.0)
	{
		return ZERO;
	}
	if (k == 4.0) 
	{
		return eisenstein4(z);
	} 
	if (k == 6.0) 
	{
		return eisenstein6(z);
	}

	if (k > 6.0)
	{
		vec2 e4 = eisenstein4(z);
		if (k == 8.0)
		{
			return cmul(e4,e4);
		}
		vec2 e6 = eisenstein6(z);
		// can probably save a lot of operations copy pasting the eisenstein4/6 code here to reuse variables
		if (k == 10.0)
			// todo: distribute the division
		{
			return cmul(e4,e6);
		} else if (k == 12.0) {
			return 0.638205499276411*cpow(e4,3.0) + 0.361794500723589 * cpow(e6,2.0);
		} else if (k == 14.0) {
			return cmul(cpow(e4,2.0), e6);
		} else if (k == 16.0) {
			return 1.0/3617.0 * (1617.0*cpow(e4,4.0) + 2000.0*cmul(e4,cpow(e6,2.0)));
		} else if (k == 18.0) {
			return 1.0/43867.0 * (38367.0*cmul(cpow(e4,3.0),e6) + 550.0 * cpow(e6,3.0));
		} else if (k == 20.0) {
			return 1.0/ 174611.0 * (53361.0*cpow(e4,5.0) + 121250.0*cmul(cpow(e4,2.0),cpow(e6,2.0)));
		} else if (k == 22.0) {
			return 1.0/77683.0*(57183.0*cmul(cpow(e4,4.0),e6) + 20500.0 * cmul(e4,cpow(e6,3.0)));
		} else if (k == 24.0) {
			return 1.0/236364091.0 * (49679091.0 * cpow(e4,6.0) + 176400000.0*cmul(cpow(e4,3.0),cpow(e6,2.0)) + 10285000.0*cpow(e6,4.0));
		}
        // todo: k > 24??
	}
	return ZERO;
}
#endfunction



// Returns eta^24 where eta is the dedekind eta function
// Note: this is the modular discriminant over (2pi)^12
#function delta
#requires cexp cpow
vec2 delta(vec2 z) {
	if (z.y <= 0.0)
	{
		return ZERO;
	}
	vec2 q = cexp(PI * vec2(-z.y,z.x));
	vec2 q4 = cexp(PI/4.0 * vec2(-z.y,z.x));

	vec2 a = ONE;
	vec2 b = ZERO;
	vec2 c = ZERO;

	float alternate_j = -1.0;
	for (int j = 1; j < THETA_BOUND; j++) {
		a += cpow(q,float(j*(j+1)));
		b += cpow(q,float(j*j));
		c += alternate_j *cpow(q,float(j*j));

		alternate_j = -alternate_j;
	}

	a = 2.0 * cmul(q4,a);
	b = 2.0 * b + ONE;
	c = 2.0 * c + ONE;

	vec2 prod = cpow(0.5*cmul(cmul(a,b),c),8.0);
	// can multiply by (2*pi)^12... or not
	return prod;
}
#endfunction



// delta(q)
#function deltaq
#requires cpow
vec2 deltaq(vec2 z) {
	if (cmag2(z) >= 1.0)
	{
		return ZERO;
	}
	vec2 q = z;
	vec2 prod = q;
	for (int j = 1; j < DELTA_BOUND; j++) {
		prod = cmul(prod,cpow(ONE-cpow(q,float(j)),24.0));
	}
	return prod;
}
#endfunction



// Returns the weierstrass p function with w1 = 1, w2 = tau
// Algorithms from equation 1.10 of https://arxiv.org/pdf/1806.06725.pdf
#function wp wpHelper weierstrassp
#requires cexp cpow

vec2 wpHelper(vec2 z, vec2 t) {
	// Manually compute thetas here since you can reuse variables instead of calling separately
	vec2 q = cexp(PI * vec2(-t.y,t.x));
	vec2 q4 = cexp(PI/4.0 * vec2(-t.y,t.x));
	vec2 w = cexp(PI * vec2(-z.y,z.x));
	vec2 v = cdiv(1.0,w);

	vec2 thetaz1 = w-v;
	vec2 theta02 = ONE;
	vec2 theta03 = ZERO;
	vec2 thetaz4 = ONE;

	float alternate_j = -1.0;
	for (int j = 1; j < THETA_BOUND; j++) {
		thetaz1 += alternate_j * cmul(cpow(q,float(j*(j+1))),cpow(w,float(2*j+1))-cpow(v,float(2*j+1)));
		theta02 += cpow(q,float(j*(j+1)));
		theta03 += cpow(q,float(j*j));
		thetaz4 += alternate_j * cmul(cpow(q,float(j*j)),cpow(w,float(2*j))+cpow(v,float(2*j)));

		alternate_j = -alternate_j;
	}

	thetaz1 = cmul(vec2(q4.y,-q4.x),thetaz1);
	theta02 = 2.0 * cmul(q4,theta02);
	theta03 = 2.0 * theta03 + ONE;

	theta02 = cmul(theta02,theta02);
	theta03 = cmul(theta03,theta03);

	// pi * pi = 9.8696044010893586188
	vec2 prod = 9.8696044010893586188*cmul(theta02,theta03);
	prod = cmul(prod, cpow(thetaz4,2.0));
	prod = cmul(prod, cpow(thetaz1,-2.0));

	theta02 = cmul(theta02,theta02);
	theta03 = cmul(theta03,theta03);

	// pi * pi / 3 = 3.2898681336964528729
	prod -= 3.2898681336964528729 * (theta02+theta03);
	return prod;
}

vec2 wp(vec2 z, vec2 t) {
	// Shift z to be in the fundamental parallelogram
	z -= floor(z.y/t.y)*t;
	z.x = fract(z.x);

	// extend domain to allow strange tau values
	if (t.y < 0.0) {
		t = -t;
	}
	float t_magnitude = length(t); // |t|
    if (t_magnitude > 1.0) { // Threshold for "large"
        vec2 t_prime = cdiv(-1.0, t);

        // Transform z: z' = z / t
        vec2 z_prime = cdiv(z, t);

        // Compute wp(z'; t') using the same function recursively
        vec2 wp_transformed = wpHelper(z_prime, t_prime);

        // Rescale back to original coordinates
        return cdiv(wp_transformed,cmul(t, t)); // wp(z; t) = t^-2 * wp(z/t; -1/t)
    } else {
		return wpHelper(z,t);
	}
}


vec2 weierstrassp(vec2 z, vec2 tau) {
	return wp(z,tau);
}
#endfunction



// benchmarking function
#function bench1000
#requires wp
vec2 bench1000(vec2 z) {
	vec2 temp = z;
	for (int j = 0; j < 100; j++) {
		temp += wp(z,rho);
	}
	return temp;
}
#endfunction



// Returns the derivative of the Weierstrass p function wp
// This satisfies p'^2 - 4p^3 +g2 p + g3 =0, although we have some instability issues
// Can check by plotting cpow(wpprime(z,rho),2.0)-4.0*cpow(wp(z,rho),3.0) + g3(rho)
// or cpow(wpprime(z,draggableArg),2.0)-4.0*cpow(wp(z,draggableArg),3.0)  +cmul(g2(draggableArg),wp(z,draggableArg)) + g3(draggableArg)
#function wpprime wpprimeHelper
#requires cexp cpow
vec2 wpprimeHelper(vec2 z, vec2 t) {
	// Shift z to be in the fundamental parallelogram
	// z -= floor(z.y/t.y)*t;
	// z.x = fract(z.x);

	// Manually compute thetas here since you can reuse variables instead of calling separately
	vec2 q = cexp(PI * vec2(-t.y,t.x));
	vec2 q4 = cexp(PI/4.0 * vec2(-t.y,t.x));
	vec2 w = cexp(PI * vec2(-z.y,z.x));
	vec2 v = cdiv(1.0,w);

	vec2 thetaz1 = w-v;
	vec2 theta02 = ONE;
	vec2 theta03 = ZERO;
	vec2 thetaz4 = ONE;

	vec2 thetaz1_prime = w+v;
	vec2 thetaz4_prime = ZERO;

	float alternate_j = -1.0;
	for (int j = 1; j < THETA_BOUND; j++) {
		thetaz1 += alternate_j * cmul(cpow(q,float(j*(j+1))),cpow(w,float(2*j+1))-cpow(v,float(2*j+1)));
		theta02 += cpow(q,float(j*(j+1)));
		theta03 += cpow(q,float(j*j));
		thetaz4 += alternate_j * cmul(cpow(q,float(j*j)),cpow(w,float(2*j))+cpow(v,float(2*j)));

		thetaz1_prime += float(2*j+1)*alternate_j * cmul(cpow(q,float(j*(j+1))),cpow(w,float(2*j+1))+cpow(v,float(2*j+1)));
		thetaz4_prime += float(2*j)*alternate_j * cmul(cpow(q,float(j*j)),cpow(w,float(2*j))-cpow(v,float(2*j)));

		alternate_j = -alternate_j;
	}

	thetaz1 = cmul(vec2(q4.y,-q4.x),thetaz1);
	theta02 = 2.0 * cmul(q4,theta02);
	theta03 = 2.0 * theta03 + ONE;

	theta02 = cmul(theta02,theta02);
	theta03 = cmul(theta03,theta03);

	thetaz1_prime = cmul(PI*q4,thetaz1_prime);
	thetaz4_prime = cmul(I*PI,thetaz4_prime);

	// pi * pi = 9.8696044010893586188
	vec2 prod = 9.8696044010893586188*cmul(theta02,theta03);
	prod = cmul(2.0*prod,thetaz4);
	prod = cmul(prod, cmul(thetaz1,thetaz4_prime)-cmul(thetaz4,thetaz1_prime));
	prod = cmul(prod,cpow(thetaz1,-3.0));
	return prod;
}

vec2 wpprime(vec2 z, vec2 t) {
	// Shift z to be in the fundamental parallelogram
	z -= floor(z.y/t.y)*t;
	z.x = fract(z.x);

	// extend domain to allow strange tau values
	if (t.y < 0.0) {
		t = -t;
	}
	float t_magnitude = length(t); // |t|
	if (t_magnitude > 1.0) { // Threshold for "large"
		vec2 t_prime = cdiv(-1.0, t);

		// Transform z: z' = z / t
		vec2 z_prime = cdiv(z, t);

		vec2 wp_transformed = wpprimeHelper(z_prime, t_prime);

		return cdiv(wp_transformed,cpow(t, 3.0)); // wp(z; t) = t^-2 * wp(z/t; -1/t)
	} else {
		return wpprimeHelper(z,t);
	}
}

#endfunction



// Weierstrass sigma function
// Uses the garbage implementation outlined at https://mathworld.wolfram.com/WeierstrassSigmaFunction.html
// which uses the garbage notation that w = e^{i z} not e^{i pi z}.
#function wsigma
#requires cexp cpow
vec2 wsigma(vec2 z, vec2 t) {
	// TODO: implement quasiperiodicity?
	vec2 q = cexp(PI * vec2(-t.y,t.x));
	vec2 q4 = cexp(PI/4.0 * vec2(-t.y,t.x));

	vec2 w = ONE;
	vec2 v = ONE;

// theta evaluated at pi z -> z
	vec2 w_PI= cexp(PI*vec2(-z.y,z.x));
	vec2 v_PI = cdiv(1.0,w_PI);

	vec2 thetaz1 = w_PI-v_PI;

// theta1'(0,q)
	vec2 thetaz1_prime = 2.0*ONE;
	vec2 thetaz1_triple_prime = 2.0*ONE;

	float alternate_j = -1.0;
	for (int j = 1; j < THETA_BOUND; j++) {
		thetaz1 += alternate_j * cmul(cpow(q,float(j*(j+1))),cpow(w_PI,float(2*j+1))-cpow(v_PI,float(2*j+1)));


		thetaz1_prime += float(2*j+1)*alternate_j * cpow(q,float(j*(j+1))) * 2.0;
		thetaz1_triple_prime += float((2*j+1)*(2*j+1)*(2*j+1))*alternate_j * cpow(q,float(j*(j+1))) * 2.0;

		alternate_j = -alternate_j;
	}

	thetaz1 = cmul(vec2(q4.y,-q4.x),thetaz1);


	thetaz1_prime =cmul(q4,thetaz1_prime);
	thetaz1_triple_prime = -cmul(q4,thetaz1_triple_prime);

	vec2 prod = cdiv(ONE,PI*thetaz1_prime);
	prod = cmul(prod,cexp(-PI*PI*cdiv(cmul(cmul(z,z),thetaz1_triple_prime),6.0*thetaz1_prime)));
	return cmul(prod,thetaz1);
}
#endfunction



// Klein's j-invariant
// Implemented via https://en.wikipedia.org/wiki/J-invariant, ``Expressions in terms of theta functions''
#function kleinj
#requires cexp cpow
vec2 kleinj(vec2 z) {
	if (z.y <= 0.0) {
		return ZERO;
	}
	// if (z.x == 0.0)
	// {
	// 	return ZERO;
	// } else if (z.y < 0.0) { //this should work
	// 	z = cdiv(-ONE,z);
	// }
	vec2 q = cexp(PI * vec2(-z.y,z.x));

	vec2 a = ONE;
	vec2 b = ZERO;
	vec2 c = ZERO;

	float alternate_j = -1.0;
	for (int j = 1; j < THETA_BOUND; j++) {
		a += cpow(q,float(j*(j+1)));
		b += cpow(q,float(j*j));
		c += alternate_j *cpow(q,float(j*j));

		alternate_j = -alternate_j;
	}

	a = 2.0 * a;
	b = 2.0 * b + ONE;
	c = 2.0 * c + ONE;

	a = cmul(q,cmul(q,cpow(a,8.0)));
	b = cpow(b,8.0);
	c = cpow(c,8.0);
	return 32.0 * cdiv(cpow(a+b+c,3.0),cmul(cmul(a,b),c));
}
#endfunction



// =j(z)/1728
#function kleinJ
#requires cexp cpow
vec2 kleinJ(vec2 z) {
	if (z.y <= 0.0)
	{
		return ZERO;
	}

	vec2 q = cexp(PI * vec2(-z.y,z.x));

	vec2 a = ONE;
	vec2 b = ZERO;
	vec2 c = ZERO;

	float alternate_j = -1.0;
	for (int j = 1; j < THETA_BOUND; j++) {
		a += cpow(q,float(j*(j+1)));
		b += cpow(q,float(j*j));
		c += alternate_j *cpow(q,float(j*j));

		alternate_j = -alternate_j;
	}

	a = 2.0 * a;
	b = 2.0 * b + ONE;
	c = 2.0 * c + ONE;

	a = cmul(q,cmul(q,cpow(a,8.0)));
	b = cpow(b,8.0);
	c = cpow(c,8.0);
	return cdiv(cpow(a+b+c,3.0),54.0*cmul(cmul(a,b),c));
}
#endfunction



// Klein's j-invariant as a function of q = exp(2 pi i z)
#function kleinjq
#requires cpow
vec2 kleinjq(vec2 q) {
	if (q.x*q.x+q.y*q.y >= 1.0) {
		return ZERO;
	}
	q = cpow(q,0.5);

	vec2 a = ONE;
	vec2 b = ZERO;
	vec2 c = ZERO;

	float alternate_j = -1.0;
	for (int j = 1; j < THETA_BOUND; j++) {
		a += cpow(q,float(j*(j+1)));
		b += cpow(q,float(j*j));
		c += alternate_j *cpow(q,float(j*j));

		alternate_j = -alternate_j;
	}

	a = 2.0 * a;
	b = 2.0 * b + ONE;
	c = 2.0 * c + ONE;

	a = cmul(q,cmul(q,cpow(a,8.0)));
	b = cpow(b,8.0);
	c = cpow(c,8.0);
	return 32.0 * cdiv(cpow(a+b+c,3.0),cmul(cmul(a,b),c));
}
#endfunction



// =kleinjq(z)/1728
#function kleinJq
#requires cpow
vec2 kleinJq(vec2 q) {
	if (q.x*q.x+q.y*q.y >= 1.0) {
		return ZERO;
	}
	q = cpow(q,0.5); //???

	vec2 a = ONE;
	vec2 b = ZERO;
	vec2 c = ZERO;

	float alternate_j = -1.0;
	for (int j = 1; j < THETA_BOUND; j++) {
		a += cpow(q,float(j*(j+1)));
		b += cpow(q,float(j*j));
		c += alternate_j *cpow(q,float(j*j));

		alternate_j = -alternate_j;
	}

	a = 2.0 * a;
	b = 2.0 * b + ONE;
	c = 2.0 * c + ONE;

	a = cmul(q,cmul(q,cpow(a,8.0)));
	b = cpow(b,8.0);
	c = cpow(c,8.0);
	return cdiv(cpow(a+b+c,3.0),54.0*cmul(cmul(a,b),c));
}
#endfunction



#function kleinj_from_g2_g3
#requires cpow
float kleinj_from_g2_g3(float a, float b) {
	return cpow(4.0*a,3.0)/(cpow(4.0*a,3.0) + cpow(27.0*b,2.0)) * 1728.0/16.0;
}
#endfunction



#function inverse_wp
#requires wp xy_in_f1_domain hypergeometricf1 cpow
const float INVERSE_WP_TOL = 0.01;
const float INVERSE_WP_DX = 0.01;
const int INVERSE_WP_GRADIENT_DESCENT_BOUND = 100;

// Invert weierstrass p by hacky gradient descent -- actually works! really slow though
// test with wp(inverse_wp(z,rho),rho)
vec2 inverse_wp(vec2 z, vec2 tau) {
	if (tau.y < 0.0) {
		tau = -tau;
	}

	vec2 r1 = wp(0.5*ONE,tau);
	vec2 r2 = wp(tau/2.0,tau);
	vec2 r3 = wp(0.5*ONE + tau/2.0,tau);

	vec2 x = cdiv(r2-r1,z-r1);
	vec2 y = cdiv(r3-r1,z-r1);

	if (xy_in_f1_domain(x,y) == 1) {
		return cdiv(-1.0*hypergeometricf1(0.5,0.5,0.5,1.5,x,y), cpow(z-r1,0.5));
	}

	vec2 a = tau/2.0 + .5*ONE;
	float ydist = cabs(z-wp(a,tau));
	float n = 0.0;
	float e = 0.0;
	float s = 0.0;
	float w = 0.0;
	float dir = 0.0;

	for (int step = 0; step < INVERSE_WP_GRADIENT_DESCENT_BOUND; step++) {
		if (ydist < INVERSE_WP_TOL) {
			return a;
		}
		// TODO: think how to reuse a calculation
		n = cabs(wp(a + INVERSE_WP_DX * tau*ydist,tau)- z);
		e = cabs(wp(a + INVERSE_WP_DX * ONE*ydist,tau)- z);
		s = cabs(wp(a - INVERSE_WP_DX * tau*ydist,tau)- z);
		w = cabs(wp(a - INVERSE_WP_DX * ONE*ydist,tau)- z);
		dir = n;
		if (dir > e) {
			dir = e;
		}
		if (dir > s) {
			dir = s;
		}
		if (dir > w) {
			dir = w;
		}
		if (dir == n) {
			a += INVERSE_WP_DX * ydist * tau;
		} else if (dir == e) {
			a += INVERSE_WP_DX * ydist * ONE;
		} else if (dir == s) {
			a -= INVERSE_WP_DX * ydist * tau;
		} else if (dir == w) {
			a -= INVERSE_WP_DX * ydist * ONE;
		}
		ydist = dir;
	}
	return ZERO;

}
#endfunction



// Inverse function to kleinJ
// Uses ``Method 4: Solving the quadratic in Î±'' from https://en.wikipedia.org/wiki/J-invariant
// inverse_j(kleinj(z)) is not an inverse tho
#function inverse_j inverse_j_reduced
#requires cpow hypergeometric2f1
vec2 inverse_j(vec2 z) {

	// Test code: 1728.0*cdiv(cpow(g2(inverse_j(z)),3.0),cpow(g2(inverse_j(z)),3.0)-27.0 * cpow(g3(inverse_j(z)),2.0))
	// Should return the identity (it does!)

	vec2 temp = cdiv(432.0,z);
	vec2 a = 0.5 * (ONE + cpow(ONE-4.0*temp,0.5));
	return cmul(I,cdiv(hypergeometric2f1(1.0/6.0,5.0/6.0,1.0,ONE-a),hypergeometric2f1(1.0/6.0,5.0/6.0,1.0,a)));
}

// specialized version for inverse_g2_g3
// NOTE: can up F21_BOUND if needed for more precision
vec2 inverse_j_reduced(vec2 z) {
	// sqrt(27) = 5.19615242270663
	vec2 a = 0.5 * (ONE + 5.19615242270663*cpow(z,0.5));
	// can maybe save some calculations doing these both at once!
	return cmul(I,cdiv(hypergeometric2f1(1.0/6.0,5.0/6.0,1.0,ONE-a),hypergeometric2f1(1.0/6.0,5.0/6.0,1.0,a)));
}

vec2 inverse_j_reduced(float z) {
	// sqrt(27) = 5.19615242270663
	float a = 0.5 * (1.0 + 5.19615242270663*csqrt(z).x);
	return cmul(I,cdiv(hypergeometric2f1(1.0/6.0,5.0/6.0,1.0,1.0-a),hypergeometric2f1(1.0/6.0,5.0/6.0,1.0,a)));
}
#endfunction



#function arithmetic_geometric_mean agm
#requires csqrt
const int ARITHMETIC_GEOMETRIC_MEAN_BOUND = 10;
const float ARITHMETIC_GEOMETRIC_MEAN_TOL = .001;

vec2 arithmetic_geometric_mean(vec2 x, vec2 y) {
	vec2 an = x;
	vec2 gn = y;
	for (int i = 0; i < ARITHMETIC_GEOMETRIC_MEAN_BOUND; i++) {
		if (cabs(an-gn) < ARITHMETIC_GEOMETRIC_MEAN_TOL) {
			break;
		}
		x = 0.5*(an+gn);
		y = csqrt(cmul(an,gn));

		an = x;
		gn = y;
	}
	return an;

}

vec2 agm(vec2 x, vec2 y) {
	return arithmetic_geometric_mean(x,y);
}
#endfunction



#function arithmetic_geometric_mean_for_g2_g3
#requires csqrt
// weird function to satisfy wikipedia's equation for fundamental periods on https://en.wikipedia.org/wiki/Elliptic_curve
vec2 arithmetic_geometric_mean_for_g2_g3(vec2 x, vec2 y) {
	vec2 an = x;
	vec2 gn = y;
	for (int i = 0; i < ARITHMETIC_GEOMETRIC_MEAN_BOUND; i++) {
		if (cabs(an-gn) < ARITHMETIC_GEOMETRIC_MEAN_TOL) {
			break;
		}
		x = 0.5*(an+gn);
		y = csqrt(cmul(an,gn));
		if (cabs(x-y) > cabs(x+y)) {
			y = -y;
		}

		an = x;
		gn = y;
	}
	
	return an;
}
#endfunction



#function in_fun_domain
bool in_fun_domain(vec2 z) {
	if (cmag2(z)<1.0) {
		return false;
	} else if (z.x < -0.5) {
		return false;
	} else if (z.x > -0.5) {
		return false;
	}
	return true;
}
#endfunction



// in: z in H
// out: z in D, i.e. |z|>1, |z.x|<.5
#function map_to_fun_domain
// how many steps to try to map to fun domain
const int MAX_FUN = 100;

vec2 map_to_fun_domain(vec2 z) {
	for (int i = 0; i < MAX_FUN; i++) {
		if (cmag2(z)<1.0) {
			z = -cinv(z);
		} else if (z.x <= -0.5) {
			z += ONE;
		} else if (z.x > 0.5) {
			z -= ONE;
		} else {
			return vec2(cabs(z.x),z.y);
		}
	}
	return z;
}
#endfunction



// IN: g2 = a, g3 = b
// OUT: tau such that y^2 = 4x^3 - g2(tau)x - g3(tau) is isomorphic to y^2 = 4x^3 - ax - b
// 

//  oh my lord it actually works
// Test with inverse_g2_g3(g2(z),g3(z))
#function inverse_g2_g3
#requires cpow csqrt arithmetic_geometric_mean arithmetic_geometric_mean_for_g2_g3 map_to_fun_domain
const float cube_root_three = 1.44224957;
const float cube_root_three_squared = 2.08008382;

const vec2 one_plus_root_three_i = vec2(1.0, 1.73205081);
const vec2 one_minus_root_three_i = vec2(1.0, -1.73205081);

vec2 inverse_g2_g3(vec2 a, vec2 b) {

	vec2 a3 = cpow(a,3.0);
	vec2 b2 = cpow(b,2.0);

	vec2 d = cpow(sqrt(3.0) *csqrt(a3 + 27.0* b2) - 9.0* b,1.0/3.0);

	// r1,r2,r3 work!
	vec2 r1 = 0.5* (d/cube_root_three_squared - cdiv(a,d)/cube_root_three);
	vec2 r2 = cdiv(cmul(one_plus_root_three_i,a),4.0*cube_root_three*d) - cmul(d, one_minus_root_three_i) /4.0 / cube_root_three_squared;
	vec2 r3 = cdiv(cmul(one_minus_root_three_i,a),4.0*cube_root_three*d) - cmul(d, one_plus_root_three_i) /4.0 / cube_root_three_squared;

	vec2 a0 = csqrt(r1-r3);
	vec2 b0 = csqrt(r1-r2);
	vec2 c0 = csqrt(r2-r3);

	d = cdiv(arithmetic_geometric_mean(c0,cmul(I,b0)),arithmetic_geometric_mean_for_g2_g3(a0,b0));
	return map_to_fun_domain(d);

	// fwiw this is a decent inverse right below the fundamental domain
	// // vec2 tau = inverse_j_reduced(cdiv(cpow(b,2.0),cpow(a,3.0)));
}

vec2 inverse_g2_g3(float a, float b) {
	return inverse_g2_g3(vec2(a, 0.0), vec2(b, 0.0));
}
#endfunction